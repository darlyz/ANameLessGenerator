\ 双曲型方程算法文件newmarkr.sch(计算位移、速度、加速度，瑞利阻尼)
\ 基于双曲型方程粘性阻尼算法newmark得到：
\     ( [S] +[M]/(γ*Δt*Δt) +[C]*β/(γ*Δt) )[Uˉ] = [Fˉ] 
\     + [M]( [U]/(γ*Δt*Δt) +[V]/(γ*Δt)  +[A]*(1/(2*γ)-1)    )
\     + [C]( [U]*β/(γ*Δt)  +[V]*(β/γ-1) +[A]*(β/(2*γ)-1)*Δt )
\ 定义瑞利阻尼为 [C] = b1[M]+b2[S] 带入上式并整理得：
\  ( [S]*(1+b2*β/(γ*Δt)) +[M]*(1/(γ*Δt*Δt)+b1*β/(γ*Δt)) )[Uˉ] = [Fˉ]
\   +[M][U]*( 1/(γ*Δt*Δt) +b1*β/(γ*Δt)       ) +[S][U]*b2*β/(γ*Δt)
\   +[M][V]*( 1/(γ*Δt)    +b1*(β/γ-1)        ) +[S][V]*b2*(β/γ-1)
\   +[M][A]*( (1/(2*γ)-1) +b1*(β/(2*γ)-1)*Δt ) +[S][A]*b2*(β/(2*γ)-1)*Δt
\ -------------------------------------------------------------------
DEFI
STIF s
MASS m
DAMP c
LOAD f
TYPE w
MDTY l
INIT 3

EQUATION
VECT u1,v1,w1
\................ 读取解空间中的 u1,v1,w1 作为上一时刻的位移、速度、加速度 ................/
READ(s,unod) u1,v1,w1
\............................... 线性方程组左端项(分布矩阵) .............................../
MATRIX = [s]*(1+b2*a1)+[m]*(a0+b1*a1)
\................................... 线性方程组右端项 ...................................../
FORC = [f]+[m*u1]*(a0+b1*a1)+[m*v1]*(a2+b1*a4)+[m*w1]*(a3+b1*a5) \
    +[s*u1]*a1*b2+[s*v1]*a4*b2+[s*w1]*a5*b2

SOLUTION u
VECT u,v,w,,u1,v1,w1
$cc // 读取上一时刻的位移、速度、加速度 u1 v1 w1
READ(s,unod) u1,v1,w1
$cc // 通过 w = (u-u1)/(γ*Δt*Δt)-v1/(γ*Δt)-w1*(1/(2*γ)-1) 计算当前加速度
[w] = ([u]-[u1])*a0-[v1]*a2-[w1]*a3
$cc // 通过 v = v1+((1-β)*w+β*w1)*Δt 计算当前速度
[v] = [v1]+[w]*a7+[w1]*a6
$cc // 存储求解的当前时刻位移、速度、加速度 u v w
WRITE (o,unod) u,v,w

@BEGIN
 double o,a0,a1,a2,a3,a4,a5,a6,a7,aa,b1,b2;
        o = 0.5;                     // o := β
        aa = 0.25*(.5+o)*(.5+o);     // aa:= γ = (1/2+β)*(1/2+β)/4
        a0 = 1./(dt*dt*aa);          // a0 = 1/(γ*Δt*Δt)
        a1 = o/(aa*dt);              // a1 = β/(γ*Δt)
        a2 = 1./(aa*dt);             // a2 = 1/(γ*Δt)
        a3 = 1./(2*aa)-1.;           // a3 = 1/(2*γ)-1
        a4 = o/aa-1.;                // a4 = β/γ-1
        a5 = dt/2*(o/aa-2.);         // a5 = (β/(2*γ)-1)*Δt
        a6 = dt*(1.-o);              // a6 = (1-β)*Δt
        a7 = dt*o;                   // a7 = β*Δt
        b1 = dampalfa;
        b2 = dampbeta;               // 瑞利阻尼系数

END
