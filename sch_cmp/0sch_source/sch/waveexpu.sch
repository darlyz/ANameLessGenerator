\ 双曲型方程显示算法文件 waveexpu.sch（先求位移再求速度，粘性阻尼）：
\ 空间离散后矩阵形式为：
\                        [M][A]+[C][V]+[S][U] = [F]
\ 其中 V=dU/dt，A=dV/dt。对加速度 A 进行时间向后差分，刚度项保留前一时刻值可得：
\                 [M]([V]-[ˉV])+[C][V]*Δt+[S][ˉU]*Δt = [F]*Δt
\ 其中 ˉV 记为 V 的前一时刻值，即 ˉV=V(t-Δt)，ˉU 同理，Δt 为时间步长。
\ 对速度 V 再次进行时间向后差分可得：
\         [M]([U]-[ˉU])-[M][ˉV]*Δt+[C]([U]-[ˉU])*Δt+[S][ˉU]*Δt*Δt = [F]*Δt*Δt
\ 移项并整理得：
\         ([M]+[C]*Δt)[U] = [M][ˉV]*Δt+([M]+[C]*Δt-[S]*Δt*Δt)[ˉU]+[F]*Δt*Δt
\ ---------------------------------------------------------------------------
DEFI
STIF s
MASS m
DAMP c
LOAD f
TYPE w
MDTY l
INIT 2

COEF u

EQUATION
VECT u,v
\........... 读取解空间中的 u,v 作为上一时刻的位移和速度 .........../
READ(s,unod) u,v
\.................. 线性方程组左端项(集中矩阵) ..................../
L,MASS = [m]+[c]*dt
\....................... 线性方程组右端项 ........................./
FORC = [m*u]+[m*v]*dt+[c*u]*dt-[s*u]*dt*dt+[f]*dt*dt

SOLUTION u1
VECT u,v u1
$cc // 通过 v = (u1-u)/Δt 计算当前速度
[v] = ([u1]-[u])/dt
$cc // 存储求解的当前时刻位移和速度 u1 v
WRITE(o,unod) u1,v

END
