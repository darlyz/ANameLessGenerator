\ 双曲型方程算法文件 wavev.sch（先求速度再求位移，粘性阻尼）：
\ 空间离散后矩阵形式为
\                  [M][A]+[C][V]+[S][U] = [F]
\ 其中 V=dU/dt，A=dV/dt。对加速度 A 和进行时间向后差分，
\ 并令 U = V*Δt+ˉU 得到
\      [M]([V]-[ˉV])+[C][V]*Δt+[S]([V]*Δt+[ˉU])*Δt = [F]*Δt
\ 整理得
\     ([M]+[C]*Δt+[S]*Δt*Δt)[V] = [F]*Δt+[M][ˉV]-[S][ˉU]*Δt
\ ----------------------------------------------------------
DEFI
STIF s
MASS m
DAMP c
LOAD f
TYPE w
MDTY l
INIT 2

EQUATION
VECT u1,v1
\.......... 读取解空间中的 u,v 作为上一时刻的位移和速度 .........../
READ(s,unod) u1,v1
\................... 线性方程组左端项(分布矩阵) .................../
MATRIX = [s]*dt*dt+[m]+[c]*dt
\....................... 线性方程组右端项 ........................./
FORC = [f]*dt+[m*v1]-[s*u1]*dt

SOLUTION v
VECT v,u1,v1
$cc // 读取上一时刻位移和速度 u1 v1
READ(s,unod) u1,v1
$cc // 通过 u1 = u1+v*Δt 计算当前位移
[u1] = [u1]+[v]*dt
$cc // 存储求解的当前时刻位移和速度 u1 v
WRITE(o,unod) u1,v

END
