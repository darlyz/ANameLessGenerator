csult3m
DEFI
DISP u,v,w,s,o,c,
VAR u1,v1,w1,s1,o1,c1,u2,v2,w2,s2,
VAR o2,c2,u3,v3,w3,s3,o3,c3,
REFC r,z,
COOR x,y,
FUNC = ekx,eky,ekxy,gmx,gmy,ex,ey,exy,
DORD 1,1,1,1,1,1,
NODE 3
$CC double pe,pv,thick,rou,alpha,fact,fact1,fact2,fx,fy,fz;
MATE pe pv thick rou alpha fx fy fz 1.0e10 0.3 0.01 1000.0 0.6 0.0 0.0 0.0
COMMON double d1,d2,d3,b1,b2,b3
$A
$CC fact = pe*thick*thick*thick/12./(1.-pv*pv);
$CC fact2 = pe/(1.+pv)/(1.-pv)*thick;
$CC b1=^coora[2][2]-^coora[2][3];
$CC b2=^coora[2][3]-^coora[2][1];
$CC b3=^coora[2][1]-^coora[2][2];
$CC d1=-^coora[1][2]+^coora[1][3];
$CC d2=-^coora[1][3]+^coora[1][1];
$CC d3=-^coora[1][1]+^coora[1][2];

SHAP
U=
u1=r
u2=z
u3=(1.-r-z)

v=
v1=r
v2=z
v3=(1.-r-z)

w=
w1=(r+r*r*z+r*r*(1-r-z)-r*z*z-r*(1-r-z)*(1-r-z))
w2=(z+z*z*(1-r-z)+z*z*r-z*(1-r-z)*(1-r-z)-z*r*r)
w3=((1-r-z)+(1-r-z)*(1-r-z)*r+(1-r-z)*(1-r-z)*z-(1-r-z)*r*r-(1-r-z)*z*z)
s1=(b2*(r*r*(1-r-z)+r*z*(1-r-z)/2)-b3*(r*r*z+r*z*(1-r-z)/2))
s2=(b3*(z*z*r+r*z*(1-r-z)/2)-b1*(z*z*(1-r-z)+r*z*(1-r-z)/2))
s3=(b1*((1-r-z)*(1-r-z)*z+r*z*(1-r-z)/2)-b2*((1-r-z)*(1-r-z)*r+r*z*(1-r-z)/2))
o1=(d2*(r*r*(1-r-z)+r*z*(1-r-z)/2)-d3*(r*r*z+r*z*(1-r-z)/2))
o2=(d3*(z*z*r+r*z*(1-r-z)/2)-d1*(z*z*(1-r-z)+r*z*(1-r-z)/2))
o3=(d1*((1-r-z)*(1-r-z)*z+r*z*(1-r-z)/2)-d2*((1-r-z)*(1-r-z)*r+r*z*(1-r-z)/2))

s=
w1=0
w2=0
w3=0
s1=r
s2=z
s3=(1.-r-z)
o1=0
o2=0
o3=0

o=
w1=0
w2=0
w3=0
s1=0
s2=0
s3=0
o1=r
o2=z
o3=(1.-r-z)

c=
c1=r
c2=z
c3=(1.-r-z)

TRAN
x=
x(1)=r
x(2)=z
x(3)=(1.-r-z)

y=
y(1)=r
y(2)=z
y(3)=(1.-r-z)

GAUS =  6
      1.  0.  0.04166667
      0.5 0.5 0.125
      0.  1.  0.04166667
      0.  0.5 0.125
      0.  0.  0.04166667
      0.5 0.  0.125

FUNC
$CC if (det<thick*thick) {
$CC fact1 =5.*pe*thick/24./(1.+pv);
$CC } else {
$CC fact1 =5.*pe*thick*thick*thick/det/24./(1.+pv);
$CC }
ekx=+[o/x]

eky=-[s/y]

ekxy=-[s/x]+[o/y]

gmx=+[w/x]+[o]

gmy=+[w/y]-[s]

ex=+[u/x]

ey=+[v/y]

exy=+[u/y]+[v/x]

STIF
DIST=
+[ekx;ekx]*fact*1.
+[ekx;eky]*fact*pv
+[eky;ekx]*fact*pv
+[eky;eky]*fact*1.
+[ekxy;ekxy]*fact*(1.-pv)/2.
+[gmx;gmx]*fact1*2.
+[gmy;gmy]*fact1*2.
+[ex;ex]*fact2*(1.)
+[ex;ey]*fact2*(pv)
+[ey;ex]*fact2*(pv)
+[ey;ey]*fact2*(1.)
+[exy;exy]*fact2*((1.-pv)/2)
+[c;c]*pe*1.0e-6

LOAD=+[u]*fx+[v]*fy+[w]*fz

END
