\ ------------------------------- equation --------------------------------------
\ For harmonic field in 3drso(three dimensional spherical coordinate system).
\ -------------------------------------------------------------------------------
DISP ax ay az u
VECT a ax ay az
COOR r s o
FUNC curlax curlay curlaz div ux uy uz
VECT curla curlax curlay curlaz
VECT gu ux uy uz
VECT div div
VECT jss fjx fjy fjz
VECT d dx dy dz
SHAP %1 %2
GAUS %3
$cc double sigma,epsilon,omega,fmu,fjxr,fjxi,fjyr,fjyi,fjzr,fjzi,pc,sym_form,jstag;
mate sigma epsilon omega fmu fjxr fjxi fjyr fjyi fjzr fjzi pc \
 5.96e7 8.8541878e-12 3.14159e2 12.56637e-7 0.0 0.0 0.0 0.0 0.0 0.0 1.0
$cc double ar,ai,br,bi,cr,ci,dr,di,er,ei,ec,dxr,dxi,dyr,dyi,dzr,dzi,loadtag;
$cc sym_form=0; // symmetric weak form when sym_form is not 0
$cc jstag=1;    // source current not in the conductor when jstag is 0
$cc if(jstag==0) jstag=0;
$cc else jstag=1;
$cc if(sym_form==0){
$cc   ar = -omega*epsilon*omega;  ai = -sigma*omega;
$cc   br = -sigma;                bi = +omega*epsilon;
$cc   cr = +sigma;                ci = -omega*epsilon;
$cc   dr = -omega*epsilon/omega;  di = -sigma/omega;
$cc   ec = +omega/fmu;
$cc   loadtag = omega;
$cc }
$cc else{
$cc   ar = br = cr = dr = +omega*epsilon;
$cc   ai = bi = ci = di = -sigma;
$cc   ec = 1/fmu;
$cc   loadtag = 1;
$cc }
$cc er=0.0;
$cc ei=1.0;
$cc if(sym_form==0){
$cp   d_i=e*jss_i;
$cc }
$cc else{
$cp   d_i=jss_i;
$cc }
@L singular.rso n

FUNC
@L vol.rso n
FVECT fe 3
@L curl.rso f fe r s o ax ay az
@W curla fe
FVECT fdiv 1
@L div.rso f fdiv r s o ax ay az
@W div fdiv
FVECT fgu 3
@L grad.rso f fgu r s o u
@w gu fgu

STIF
DIST=+[a_i;a_i]*vol*omega*|ar;ai|
+[a_i;gu_i]*vol*omega*|br;bi|
+[gu_i;a_i]*vol*omega*|cr;ci|
+[gu_i;gu_i]*vol*omega*|dr;di|
+[curla_i;curla_i]*vol*|ec;0|
+[div;div]*vol*pc*|ec;0|

LOAD=+[ax]*vol*loadtag*|fjxr;fjxi|
+[ay]*vol*loadtag*|fjyr;fjyi|
+[az]*vol*loadtag*|fjzr;fjzi|
+[ux]*vol*jstag*|dxr;dxi|
+[uy]*vol*jstag*|dyr;dyi|
+[uz]*vol*jstag*|dzr;dzi|

END