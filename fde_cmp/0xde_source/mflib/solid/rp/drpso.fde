\ ..................................................................
\ sd = pa*se**pm     se*se = eij*eij*2/3
\ d = 2/3*pa*se**(pm-1)*e
\ equation in weak form
\ (d,de) = (f,du)
\ (2/3*pa*se**(pm-1)*e(u),de) = (f,du)
\ f(u) = (2/3*pa*se**(pm-1)*e(u),de) - (f,du)
\ by newton method
\ df(u)w = (2/3*pa*se**(pm-1)*e,de)
\        + (2/3*2/3*pa*(pm-1)*se**(pm-3)*e(u)*e,e(u)*de)
\ f(u+w) = f(u) + df(u)(u+w) - df(u)u = 0
\ df(u)(u+w) = df(u)u - f(u)
\            = (2/3*pa*(pm-1)*se**(pm-3)*e(u)*e(u),e(u)*de) + (f,du)
\            = (2/3*pa*(pm-1)*se**(pm-1),e(u)*de) + (f,du)
\ ..................................................................
DISP u v p a
COOR s o
VECT u u v
VECT f fx fy
COEF un vn
SHAP %1 %2
SHAP %1 %2c a_p 1 2 3 4
GAUS %3
$CC double r,pa,pm,fx,fy,rou,pm1,pm3,emid,se,pk,fact1;
$CC double enx,eny,enxx,enyy,enxy,enyz,enxz,enyx,enzy,enzx,exyz;
MASS %1 rou*vol rou*vol 0 0
@L singular.so n
MATE r pa pm fx fy rou 1.0 1.0e10 3.0 0.0 0.0 7800.0
$CC pk=pa/(pm+1.);
$CC pa=pa*2./3.;
FUNC exx eyy exy exz eyz exyz div fpa
VECT ev exx eyy
VECT ep2 exz eyz
VECT ep1 exy
VECT ep exy exz eyz
VECT exyz exyz
VECT div div
VECT en enxx enxy enyx enyy enxz enyz
VECT env enxx enyy
VECT enp1 (enxy+enyx)/2.
VECT enp2 enxz enyz
VECT enp (enxy+enyx)/2. enxz enyz

FUNC
@L vol.so n
@L gradv.so v en r s o un vn
$CV emid=+env_i*env_i
$CV emid=+emid+enp_i*enp_i*2.
$CC emid = emid*2.0/3.+1.0e-010;
$CC se = sqrt(emid);
$CC pm1 =pm-1.;
$CC fact=pa*pow(se,pm1)*vol;
$CC pm3 =pm-3.;
$CC fact1=pa*2./3.*(pm-1.)*pow(se,pm3)*vol;
FVECT fe 6
FVECT fep1 1
FVECT fep2 2
FVECT fev 2
FVECT fep 2
FVECT fexyz 1
FVECT fdiv 1
@L gradv.so f fe r s o u v
@S fev fe 1 4
@W ev fev
@S fep fe 2 3
@A fep1_j = +[fep_i]
@W ep1 fep1
@S fep2 fe 5 6
@W ep2 fep2
@A fexyz_i = [fev_j]*env_j+[fep1_j]*enp1_j+[fep2_j]*enp2_j
@W exyz fexyz
@L div.so f fdiv
@W div fdiv
fpa = [p]-[a]

STIF
DIST = +[ev_i;ev_i]*fact+[ep_i;ep_i]*fact/2.+[exyz;exyz]*fact1
       +[div;p]*vol-[p;div]*vol+[fpa;fpa]*vol

LOAD = +[u_i]*f_i*vol+[exyz]*fact*(pm-1.)
\+[ev_i]*fnv_i+[ep_i]*fnp_i

END
