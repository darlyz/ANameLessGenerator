\ ................ finite deformation equation .........
\ sij,j + fi = 0
\ s = D*e, where eij = (ui,j+uj,i)*aij + uk,i*uk,j*aij
\ aij = 1/2 if i=j else aij=1
\ where u denote displacement, s denote stress, e denote strain
\ D denote constitutive matrix
\ ......................................................
\ PDE in weak form
\ (D*e,de) = (f,du)
\ where de denotes the variation of e
\ Linearizing e in the PDE
\ e(u) = el(u)+en(u,u)
\ where
\ el(u) = (ui,j+uj,i)*aij
\ en(u,v) =  uk,i*vk,j*aij
\ ......................................................
\ e(un+du) = el(un+du) + en(du,un) + en(un,du) + en(un,un) + en(du,du)
\         = el(un+du) + en(un+du,un) + en(un,un+du) - en(un,un) + en(du,du)
\ cancel the second order term en(du,du) we have
\ e(u) = el(u) + en(u,un) + en(un,u) - en(un,un)
\ where u = un+du
\ de = el(du) + en(du,un) + en(un,du)
\ the Linearizing PDE in the weak form
\ ......................................................
\ (D*L(u),L(du)) = (D*enn,L(du)) + (f,du)
\ ......................................................
\ where
\ L(v) = el(v) + en(v,un) + en(un,v)
\ un is the previous iteration value of u
\ enn = en(un,un)
\ Den = D*enn
\ ......................................................
DISP u v w
VECT u u v w
COOR r s o
COEF un vn wn
FUNC exx eyy ezz eyz exz exy
ARRAY ^enn[3][3],^en[3][3],denv[3]
VECT env ^enn[1][1] ^enn[2][2] ^enn[3][3]
VECT enp ^enn[2][3] ^enn[1][3] ^enn[1][2]
VECT ev exx eyy ezz
VECT ep eyz exz exy
VECT f fx fy fz
SHAP %1 %2
GAUS %3
$CC double pe,pv,fx,fy,fz,rou,alpha;
MATE pe pv fx fy fz rou alpha 1.0e10 0.3 0.0 0.0 0.0 3000.0 0.6
MASS %1 rou*vol rou*vol rou*vol
DAMP %1 rou*alpha*vol rou*alpha*vol rou*alpha*vol
@L singular.rso n
MATRIX sm 3 3
(1.-pv) pv pv
pv (1.-pv) pv
pv pv (1.-pv)


FUNC
@L vol.rso n
$CC fact = pe/(1.+pv)/(1.-2.*pv)*vol;
$CC shear = (0.5-pv);
@L gradv.rso m ^en r s o un vn wn
$CV ^enn_i_j = +^en_k_i*^en_k_j
$CV denv_i = +sm_i_j*env_j/2.
FMATR fl 3 3
@L gradv.rso f fl
@A fl_i_j = +[fl_i_j]+[fl_k_i]*^en_k_j
@W ev fl 1 5 9
@A fl_i_j = +[fl_i_j]+[fl_j_i]
@W ep fl 6 3 2
STIF
DIST = +[ev_i;ev_j]*sm_i_j*fact+[ep_i;ep_i]*shear*fact

LOAD = +[u_i]*f_i*vol
       +[ev_i]*denv_i*fact
       +[ep_i]*enp_i*shear*fact

END
