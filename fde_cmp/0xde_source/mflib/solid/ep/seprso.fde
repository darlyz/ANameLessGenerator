\3d elasto_plastic model for stress
DISP sxx syy szz syz sxz sxy sq did
COOR r s o
COEF un vn wn
COMMON extern double *inelm%1%2
VECT s sxx syy szz syz sxz sxy
\ .... where ddf denote D*df/ds
VECT ddfv ddf[1] ddf[2] ddf[3]
VECT ddfp ddf[4] ddf[5] ddf[6]
VECT uk sq did
VECT fk str[7] eid
VECT str str[1] str[2] str[3] str[4] str[5] str[6]
VECT strv str[1] str[2] str[3]
VECT strp str[4] str[5] str[6]
VECT dsv ds[1] ds[2] ds[3]
VECT dsp ds[4] ds[5] ds[6]
SHAP %1 %2
GAUS %3
$CC double pe,pv,fx,fy,fz,qnn,rou,alpha,t0,time1,a,b,prag,dln,eid,cv,cp,epp,eyp,wpp;
$CC int kq,ntime,ialpha;
$CC extern double getyield(double *,double *,double *,double,double *,
$CC int,double (*f)(),int);
$CC extern double %4(double *,double *);
MASS %1 vol
ARRAY dev[3],dep[3],ds[6],dspv[3],dspp[3],dpv[3],dpp[3],^e[3][3],^d[3][3]
$CC double ddf[7],p[5],str[8];
@L singular.rso n
MATE pe pv fx fy fz p[1] p[2] p[4] qnn rou alpha \
1.0e10;0.3;0;0;0;0.2;1000;0;1;3000;0.6;
$CC kq=qnn+0.5;
$CC p[3]=pv;
MATRIX sm 3 3
(1.-pv) pv pv
pv (1.-pv) pv
pv pv (1.-pv)


STIF
$CC fact=pe/(1.0+pv)/(1.0-2.0*pv);
$CC shear=(0.5-pv)*fact;
$CV ^d_i_j=+sm_i_j*fact
@L vol.rso n
$CC j = (num-1)*ngaus*7+(igaus-1)*7;
$CC for (i=1; i<=7; ++i) str[i]=inelm%1%2[j+i];
@L gradv.rso m ^e r s o un vn wn
$CV dev_i=+^e_i_i
$CC dep[1] = ^e[2][3]+^e[3][2];
$CC dep[2] = ^e[1][3]+^e[3][1];
$CC dep[3] = ^e[1][2]+^e[2][1];
$CV dsv_i=+^d_i_j*dev_j
$CV dsp_i=shear*dep_i
$CV str_i=str_i+ds_i
$CC ialpha=0;
$CC a=getyield(p,str,d,shear,ddf,kq,%4,ialpha);
$CC prag=%4(p,str);
$CV dln=+ddfv_i*dev_i+ddfp_i*dep_i
$CC dln=dln+prag;
$CC if (dln>0.0) {
$CC  b=1.0/a;
$CC  eid=-1.0;
$CV dsv_i=+b*ddfv_i*ddfv_j*dev_j
$CV dsp_i=+b*ddfp_i*ddfp_i*dep_i
$CV strv_i=strv_i-dsv_i-ddfv_i*prag*b
$CV strp_i=strp_i-dsp_i-ddfp_i*prag*b
$CC a=getyield(p,str,d,shear,ddf,kq,%4,ialpha);
\ .... inner variable computation
$CV cv=+ddfv_i*dev_i
$CV cp=+ddfp_i*dep_i
$CV dspv_i=ddfv_i*(cv+cp)*b
$CV dspp_i=ddfp_i*(cv+cp)*b
$CC  det = invm(3,d,e);
$CV ^d_i_j = ^e_i_j
$CV dpv_i=+^d_i_j*dspv_j
$CV dpp_i=dspp_i/shear
$CC switch (kq)
$CC {
$CC case 2:
$CV epp=+dpv_i
$CC str[7]=epp+str[7];
$CC  break;
$CC case 3:
$CV eyp=+dpv_i*dpv_i+dpp_i*dpp_i
$CC str[7]=sqrt(eyp)+str[7];
$CC  break;
$CC default:
$CV wpp=+dpv_i*strv_i+dpp_i*strp_i
$CC str[7]=wpp+str[7];
$CC  break;
$CC }
$CC } else {
$CC  b=0.0;
$CC  eid=1.0;
$CC }
$CC j = (num-1)*ngaus*7+(igaus-1)*7;
$CC for (i=1; i<=7; ++i) inelm%1%2[j+i]=str[i];
DIST=+[sxx;sxx]*0.0

LOAD=+[s_i]*str_i*vol+[uk_i]*fk_i*vol

END
