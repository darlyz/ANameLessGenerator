\--------------------------equation-----------------------------------------------
\For incompressible fluid in 2dro(two dimensional polar coordinate system).
\----------------------------------------------------------------------------------
DISP u,v,p
COOR r o
SHAP %1 %2
SHAP %1 %4 p 1 2 3 4
GAUS %3
COEF un,vn
MASS %1 rou*vol rou*vol 0
FUNC ux uy vx vy cu cv div
$CC double rou,emu,fu,fv,fx,fy,gx,gy,floadx,floady,c2,bata;
ARRAY ^gun[2][2]
MATE rou emu fx fy 1.0 0.1 0.0 0.0
@L singular.ro n
VECT un un vn
VECT e ux uy vx vy
VECT div div
VECT u u v
VECT x r o
VECT f fx fy
VECT g gx gy
VECT fload floadx floady
VECT conv cu cv
$CC c2 = 1.0e-8;

FUNC
@L vol.ro n
$CC bata = det*c2;
@L gradv.ro m ^gun r o un vn
$CV g_i = +^gun_i_j*un_j
$CV fload_i = f_i+g_i
FVECT fdiv 1
@L div.ro f fdiv
@W div fdiv
FMATR fgu 2 2
@L gradv.ro f fgu r o u v
@W e fgu
FVECT fconv 2
@A fconv_i = +[fgu_i_j]*un_j
@W conv fconv

STIF
DIST=+[e_i;e_i]*emu*vol+[conv_i;u_i]*rou*vol+[u_j;u_i]*^gun_i_j*rou*vol
     +[div;p]*vol-[p;div]*vol
     +[p;p]*bata*vol

LOAD=+[u_i]*fload_i*rou*vol

END

