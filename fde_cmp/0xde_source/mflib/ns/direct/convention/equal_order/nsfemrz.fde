\--------------------------equation-----------------------------------------------
\For incompressible fluid in 2drz(two dimensional axial symmetry coordinate system).
\----------------------------------------------------------------------------------
DISP u,v,p,a
COOR r z
SHAP %1 %2
SHAP %1 %2c a_p 1 2 3 4
GAUS %3
COEF un,vn
MASS %1 rou*vol rou*vol 0 0
FUNC ux uy vx vy wz cu cv div pa
$CC double rou,emu,fx,fy,penalty,gx,gy,floadx,floady,unr,unz,vnr,vnz,wno;
MATE rou emu fx fy penalty 1.0 0.1 0.0 0.0 1.0e3
@L singular.rz n
VECT un un vn
VECT e ux uy vx vy wz
VECT div div
VECT u u v
VECT x r z
VECT f fx fy
VECT g gx gy
VECT fload floadx floady
VECT conv cu cv
VECT vgun unr unz vnr vnz wno
MATRIX mgun
 unr unz
 vnr vnz
 
 
FUNC
@L vol.rz n
@L gradv.rz v vgun r z un vn
$CV g_i = +mgun_i_j*un_j
$CV fload_i = f_i+g_i
FVECT fdiv 1
@L div.rz f fdiv
@W div fdiv
FVECT fvgu 5
@L gradv.rz f fvgu r z u v
@W e fvgu
FMATR fmgu 2 2
@S fmgu fvgu 1 2 3 4
FVECT fu 2
@R fu_i = +[u_i]
FVECT fconv 2
@A fconv_i=+[fmgu_i_j]*un_j+[fu_j]*mgun_i_j
@W conv fconv
pa = +[p]-[a]

STIF
DIST=+[e_i;e_i]*emu*vol+[conv_i;u_i]*rou*vol
     +[div;p]*vol-[p;div]*vol
     +[pa;pa]*penalty*vol

LOAD=+[u_i]*fload_i*rou*vol

END

