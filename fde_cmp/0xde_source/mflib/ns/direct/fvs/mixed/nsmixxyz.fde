\--------------------------equation-----------------------------------------------
\For incompressible fluid in 3dxyz(three dimensional cartesian coordinate system).
\continuity equation:
\                      div(u) = 0
\momentum equation:
\       rou*u,t - div(emu*grad(u)) + rou*grad(u)*u + grad(p) = f
\ where rou is density;emu is dynamic viscosity coefficient.
\ where u:(u,v,w),denote the velocity of x, y and z directions.
\ where f is vector,denote fx,fy and fz.
\--------------------the pde in weak form--------------------------------------------
\       [rou*u,t;u]+[emu*grad(u);grad(u)]+[rou*grad(un)*u;u]-
\       [p;div(u)]+[div(u);p]+[bata*p;p]=+rou*[f;u]+[rou*grad(un)*un;u] 
\ where [ . ; . ] denote the inner produc of two functions in the solution domain. 
\ $FVS will be used for the convection term (rou*grad(u)*un)
\ where un is vector,denote known un,vn and wn.
\------------------------------------------------------------------------------------
DISP u,v,w,p
COOR x,y,z
SHAP %1 %2
SHAP %1 %4 p 1 2 3 4 5 6 7 8
GAUS %3
COEF un,vn,wn
MASS %1 rou*vol rou*vol rou*vol 0
FUNC ux uy uz vx vy vz wx wy wz div
$CC double rou,emu,fu,fv,fw,fx,fy,fz,fgx,fgy,fgz,gx,gy,gz,c2,bata;
ARRAY ^gun[3][3]
MATE rou emu fx fy fz 1.0 0.1 0.0 0.0 0.0
@L singular.xyz n
$FVS ns%1%2
VECT un un vn wn
VECT e ux uy uz vx vy vz wx wy wz
VECT div div
VECT u u v w
VECT x x y z
VECT f fx fy fz
VECT g gx gy gz
VECT fg fgx fgy fgz
$CC c2 = 1.0e-8;

FUNC
@L vol.xyz n
$CC bata = pow(det,2.0/3.0)*c2;
@L gradv.xyz m ^gun x y z un vn wn
$CV g_i = +^gun_i_j*un_j
$CV fg_i = f_i+g_i
FVECT fdiv 1
@L div.xyz f fdiv
@W div fdiv
FMATR fgu 3 3
@L gradv.xyz f fgu x y z u v w
@W e fgu

STIF
DIST=+[e_i;e_i]*emu*vol+[u_j;u_i]*^gun_i_j*rou*vol
     +[div;p]*vol-[p;div]*vol
     +[p;p]*bata*vol

LOAD=+[u_i]*fg_i*rou*vol

END

