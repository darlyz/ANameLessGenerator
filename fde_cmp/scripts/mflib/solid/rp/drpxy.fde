\ ------------------------ Rigid-plastic equation ----------------------------------
\        sd = pa*se**pm
\        se*se = eij*eij*2/3
\        d = 2/3*pa*se**(pm-1)*e
\ where sd          .... effective stress;
\       se          .... effective strain rate;
\       e           .... strain;
\       pa and pm   .... constants.
\ ----------------------------------------------------------------------------------
\        (d;de) = (f;du)
\        (2/3*pa*se**(pm-1)*e(u);de) = (f;du)
\ where ( . ; . ) denotes the inner product of two functions in the solution domain.
\ ----------------------------------------------------------------------------------
\ Operator:
\        F(u) = (2/3*pa*se**(pm-1)*e(u);de) - (f;du)
\ ----------------------------------------------------------------------------------
\        F(u+w) = F(u) + dF(u)(u+w) - dF(u)u = 0
\        dF(u)(u+w) = dF(u)u - F(u)
\
\        dF(u)u = (2/3*pa*se**(pm-1)*e(u);de)
\                 + (2/3*2/3*pa*(pm-1)*se**(pm-3)*e(u)*e(u);e(u)*de)
\        
\        dF(u)(u+w) = dF(u)u - F(u)
\                   = (2/3*pa*(pm-1)*se**(pm-3)*e(u)*e(u);e(u)*de) + (f;du)
\ ----------------------------------------------------------------------------------
DISP u v p a
COOR x y
VECT u u v
VECT f fx fy
COEF un vn
SHAP %1 %2
SHAP %1 %2c a_p 1 2 3 4
GAUS %3
$CC double pa,pm,fx,fy,rou,pm1,pm3,emid,se,pk,fact1;
$CC double enx,eny,enxx,enyy,enxy,enyx;
MASS %1 rou*vol rou*vol 0 0
@L singular.xy n
MATE pa pm fx fy rou 1.0e10 3.0 0.0 0.0 7800.0
$CC pk=pa/(pm+1.);
$CC pa=pa*2./3.;
FUNC exx eyy exy exyz div fpa
VECT ev exx eyy
VECT ep exy
VECT exyz exyz
VECT div div
MATRIX en 2 2
enxx enxy
enyx enyy

VECT env enxx enyy
VECT enp (enxy+enyx)/2.

FUNC
@L vol.xy n
@L gradv.xy m en x y un vn
$CV emid=+env_i*env_i+enp_i*enp_i*2.
$CC emid = emid*2.0/3.+1.0e-010;
$CC se = sqrt(emid);
$CC pm1 =pm-1.;
$CC fact=pa*pow(se,pm1)*vol;
$CC pm3 =pm-3.;
$CC fact1=pa*2./3.*(pm-1.)*pow(se,pm3)*vol;
FMATR fe 2 2
FMATR fp 2 2
FVECT fev 2
FVECT fep 1
FVECT fexyz 1
FVECT fdiv 1
FVECT fgp 2
@L gradv.xy f fe
@A fp_i_j = +[fe_i_j]+[fe_j_i]
@S fev fe 1 4
@S fep fp 2
@W ev fev
@W ep fep
@A fexyz_i = [fev_j]*env_j+[fep_j]*enp_j
@W exyz fexyz
@L div.xy f fdiv
@W div fdiv
fpa = [p]-[a]

STIF
DIST = +[ev_i;ev_i]*fact+[ep_i;ep_i]*fact/2.+[exyz;exyz]*fact1
       +[div;p]*vol-[p;div]*vol+[fpa;fpa]*vol

LOAD = +[u_i]*f_i*vol+[exyz]*fact*(pm-1.)
\+[ev_i]*fnv_i+[ep_i]*fnp_i

END
