\--------------------------equation-----------------------------------------------
\For incompressible fluid in 2dro(two dimensional polar coordinate system).
\----------------------------------------------------------------------------------
DISP u,v,p,a
COOR r o
SHAP %1 %2
SHAP %1 %2c a_p 1 2 3 4
GAUS %3
COEF un,vn
MASS %1 rou*vol rou*vol 0 0
FUNC ux uy vx vy div pa
$CC double rou,emu,fx,fy,penalty,gx,gy,fgx,fgy;
ARRAY ^gun[2][2]
MATE rou emu fx fy penalty 1.0 0.1 0.0 0.0 1.0e3
@L singular.ro n
$FVS nsxy%1
VECT un un vn
VECT e ux uy vx vy
VECT div div
VECT u u v
VECT x r o
VECT f fx fy
VECT g gx gy
VECT fg fgx fgy

FUNC
@L vol.ro n
@L gradv.ro m ^gun r o un vn
$CV g_i = +^gun_i_j*un_j
$CV fg_i = f_i+g_i
FVECT fdiv 1
@L div.ro f fdiv
@W div fdiv
FMATR fgu 2 2
@L gradv.ro f fgu r o u v
@W e fgu
pa = +[p]-[a]

STIF
DIST=+[e_i;e_i]*emu*vol+[u_j;u_i]*^gun_i_j*rou*vol
     +[div;p]*vol-[p;div]*vol+[pa;pa]*penalty*vol

LOAD=+[u_i]*fg_i*rou*vol

END

