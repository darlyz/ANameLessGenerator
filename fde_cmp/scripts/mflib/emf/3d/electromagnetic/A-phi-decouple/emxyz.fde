\ ------------------------------- equation --------------------------------------
\ For harmonic field in 3dxyz(three dimensional cartesian coordinate system).
\ Computing electric field E and magnectic field H by the following equations
\                           E = -i*omega*A-grad(u)
\                           H = curl(A)/fmu
\ -------------------------------------------------------------------------------
DISP ex ey ez hx hy hz
VECT e ex ey ez
VECT h hx hy hz
COEF axr axi ayr ayi azr azi ur ui
VECT ar axr ayr azr
VECT ai axi ayi azi
COOR x y z
SHAP %1 %2
GAUS %3
$CC double omega,fmu;
$CC double curlaxr,curlayr,curlazr,curlaxi,curlayi,curlazi,hxr,hyr,hzr,hxi,hyi,hzi;
$CC double guxr,guyr,guzr,guxi,guyi,guzi,exr,eyr,ezr,exi,eyi,ezi,er,ei,hr,hi;
MASS %1 vol vol vol vol vol vol
VECT curlar curlaxr curlayr curlazr
VECT curlai curlaxi curlayi curlazi
VECT gur guxr guyr guzr
VECT gui guxi guyi guzi
VECT js fjx fjy fjz
VECT d dx dy dz
MATE omega fmu 3.14159e2 12.56637e-7
@L singular.xyz n
VECT hr hxr hyr hzr
VECT hi hxi hyi hzi
VECT er exr eyr ezr
VECT ei exi eyi ezi

FUNC
@L vol.xyz n
@L curl.xyz v curlar x y z axr ayr azr
@L curl.xyz v curlai x y z axi ayi azi
$CV hr_i=curlar_i/fmu
$CV hi_i=curlai_i/fmu
@L grad.xyz v gur x y z ur
@L grad.xyz v gui x y z ui
$CV er_i=-gur_i+omega*ai_i
$CV ei_i=-gui_i-omega*ar_i

STIF
DIST=+[ex;ex]*|0;0|

LOAD=+[hx]*|hxr;hxi|+[ex]*vol*|exr;exi|
+[hy]*|hyr;hyi|+[ey]*vol*|eyr;eyi|
+[hz]*|hzr;hzi|+[ez]*vol*|ezr;ezi|
\+[h_i]*|hr;hi|+[e_i]*vol*|er;ei|

END
